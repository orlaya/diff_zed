(comment) @comment @spell

; ———
; source: value
; expression: value
; ———
(frontmatter_delimiter) @punctuation.delimiter
(header_field
  (header_name) @title
  (header_colon) @punctuation.delimiter
  (header_value) @diff.delta)

[
  (addition)
  (new_file)
] @diff.plus

[
  (deletion)
  (old_file)
] @diff.minus

(commit) @constant

(location) @attribute

(command
  "diff" @function
  (argument) @variable.parameter)

(filename) @string.special.path

(mode) @number

([
  ".."
  "+"
  "++"
  "+++"
  "++++"
  "-"
  "--"
  "---"
  "----"
] @diff.delta
  (#set! priority 95))

[
  (binary_change)
  (similarity)
  (file_change)
] @label

(index
  "index" @keyword)

(similarity
  (score) @number
  "%" @number)
